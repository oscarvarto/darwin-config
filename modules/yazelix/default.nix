{
  config,
  lib,
  pkgs,
  ...
}:
with lib; let
  cfg = config.programs.yazelix;

  boolToToml = value:
    if value
    then "true"
    else "false";

  escapeString = value: let
    safe = lib.replaceStrings ["\"" "\\"] ["\\\"" "\\\\"] value;
  in "\"${safe}\"";

  listToToml = values:
    if values == []
    then "[]"
    else "[ " + (concatStringsSep ", " (map escapeString values)) + " ]";

  configDir = ./config;
  yazelixToml = let
    editorCommand =
      if cfg.editor_command != null
      then cfg.editor_command
      else "";
    editorTheme =
      if cfg.helix_theme != null
      then cfg.helix_theme
      else "";
    editorCommandKey = cfg.helix_command_key;
  in
    lib.concatStringsSep "\n" [
      "# Generated by the Yazelix Home Manager module."
      "# Edit your Home Manager configuration instead of this file."
      ""
      "[core]"
      "recommended_deps = ${boolToToml cfg.recommended_deps}"
      "yazi_extensions = ${boolToToml cfg.yazi_extensions}"
      "yazi_media = ${boolToToml cfg.yazi_media}"
      "debug_mode = ${boolToToml cfg.debug_mode}"
      "skip_welcome_screen = ${boolToToml cfg.skip_welcome_screen}"
      ""
      "[editor]"
      "command = ${escapeString editorCommand}"
      "theme = ${escapeString editorTheme}"
      "command_key = ${escapeString editorCommandKey}"
      "enable_sidebar = ${boolToToml cfg.enable_sidebar}"
      ""
      "[shell]"
      "default_shell = ${escapeString cfg.default_shell}"
      "extra_shells = ${listToToml cfg.extra_shells}"
      "enable_atuin = ${boolToToml cfg.enable_atuin}"
      ""
      "[terminal]"
      "preferred_terminal = ${escapeString cfg.preferred_terminal}"
      "extra_terminals = ${listToToml cfg.extra_terminals}"
      "config_mode = ${escapeString cfg.terminal_config_mode}"
      "cursor_trail = ${escapeString cfg.cursor_trail}"
      "transparency = ${escapeString cfg.transparency}"
      ""
      "[zellij]"
      "disable_tips = ${boolToToml cfg.disable_zellij_tips}"
      "rounded_corners = ${boolToToml cfg.zellij_rounded_corners}"
      "persistent_sessions = ${boolToToml cfg.persistent_sessions}"
      "session_name = ${escapeString cfg.session_name}"
      "widget_tray = ${listToToml cfg.widget_tray}"
      "theme = ${escapeString cfg.zellij_theme}"
      ""
      "[yazi]"
      "plugins = ${listToToml cfg.yazi_plugins}"
      "theme = ${escapeString cfg.yazi_theme}"
      "sort_by = ${escapeString cfg.yazi_sort_by}"
      ""
      "[ascii]"
      "mode = ${escapeString cfg.ascii_art_mode}"
      ""
    ]
    + "\n";
  configStore = pkgs.runCommand "yazelix-config" {} ''
        mkdir -p "$out"
        cp -R ${configDir}/. "$out/"
        cat > "$out/yazelix.toml" <<'EOF'
    ${yazelixToml}
    EOF
  '';
in {
  options.programs.yazelix = {
    enable = mkEnableOption "Yazelix terminal environment";

    # Configuration options (mirrors yazelix_default.toml structure)
    recommended_deps = mkOption {
      type = types.bool;
      default = true;
      description = "Install recommended productivity tools (~350MB)";
    };

    yazi_extensions = mkOption {
      type = types.bool;
      default = true;
      description = "Install Yazi file preview extensions (~125MB)";
    };

    yazi_media = mkOption {
      type = types.bool;
      default = false;
      description = "Install Yazi media processing tools (~1GB)";
    };

    default_shell = mkOption {
      type = types.enum ["nu" "bash" "fish" "zsh"];
      default = "nu";
      description = "Default shell for Zellij sessions";
    };

    extra_shells = mkOption {
      type = types.listOf (types.enum ["fish" "zsh"]);
      default = [];
      description = "Additional shells to install beyond nu/bash";
    };

    preferred_terminal = mkOption {
      type = types.enum ["wezterm" "ghostty" "kitty" "alacritty" "foot"];
      default = "ghostty";
      description = "Preferred terminal emulator for launch commands";
    };

    terminal_config_mode = mkOption {
      type = types.enum ["auto" "user" "yazelix"];
      default = "yazelix";
      description = ''
        How Yazelix selects terminal configs:
        - "yazelix": use Yazelix-managed configs in ~/.local/share/yazelix (default)
        - "auto": prefer user configs if present, otherwise Yazelix configs
        - "user": always use user configs (e.g., ~/.config/ghostty/config)
      '';
    };

    extra_terminals = mkOption {
      type = types.listOf (types.enum ["wezterm" "kitty" "alacritty" "foot"]);
      default = [];
      description = "Additional terminal emulators to install beyond Ghostty";
    };

    cursor_trail = mkOption {
      type = types.enum ["blaze" "snow" "cosmic" "ocean" "forest" "sunset" "neon" "party" "eclipse" "dusk" "orchid" "reef" "inferno" "random" "none"];
      default = "random";
      description = ''
        Cursor trail preset.
        Supported by all terminal emulators: "none"
        Supported by Ghostty: "blaze", "snow", "cosmic", "ocean", "forest", "sunset", "neon", "party", "eclipse", "dusk", "orchid", "reef", "inferno", "random"
        Supported by Ghostty and Kitty: "snow"
        "random" chooses a different Ghostty trail each generation (excluding "none" and "party")
      '';
    };

    transparency = mkOption {
      type = types.enum ["none" "very_low" "low" "medium" "high" "very_high" "super_high"];
      default = "medium";
      description = ''
        Terminal transparency level for all terminals.

        - "none": No transparency (opacity = 1.0)
        - "very_low": Minimal transparency (opacity = 0.95)
        - "low": Light transparency (opacity = 0.90)
        - "medium": Medium transparency (opacity = 0.85)
        - "high": High transparency (opacity = 0.80)
        - "very_high": Very high transparency (opacity = 0.70)
        - "super_high": Maximum transparency (opacity = 0.60)
      '';
    };

    # Editor configuration
    editor_command = mkOption {
      type = types.nullOr types.str;
      default = null;
      description = ''
        Editor command - yazelix will always set this as EDITOR.

        - null (default): Use yazelix's Nix-provided Helix - full integration
        - "nvim": Use Neovim - first-class support with full integration
        - "hx": Use system Helix from PATH
        - Other editors: "vim", "nano", "emacs", etc. (basic integration only)
      '';
    };

    helix_theme = mkOption {
      type = types.nullOr types.str;
      default = null;
      description = "Helix theme to apply inside Yazelix (e.g., catppuccin-mocha).";
    };

    helix_command_key = mkOption {
      type = types.str;
      default = ":";
      description = "Helix command-mode key used by Yazelix integrations (set to \";\" if you remap command_mode).";
    };

    enable_sidebar = mkOption {
      type = types.bool;
      default = true;
      description = "Enable or disable the Yazi sidebar";
    };

    disable_zellij_tips = mkOption {
      type = types.bool;
      default = true;
      description = "Disable Zellij tips popup on startup for cleaner launches";
    };

    zellij_rounded_corners = mkOption {
      type = types.bool;
      default = true;
      description = "Enable rounded corners for Zellij pane frames";
    };

    widget_tray = mkOption {
      type = types.listOf (types.enum ["layout" "editor" "shell" "term" "cpu" "ram"]);
      default = ["layout" "editor" "shell" "term" "cpu" "ram"];
      description = ''
        Widgets to display in the Zellij status bar (zjstatus).
        Available widgets: layout, editor, shell, term, cpu, ram.
        Order determines display order from left to right.
      '';
    };

    zellij_theme = mkOption {
      type = types.str;
      default = "default";
      description = ''
        Zellij color theme.
        Cross-compatible themes (work with Yazi and Helix too):
        catppuccin-frappe, catppuccin-latte, catppuccin-macchiato, catppuccin-mocha,
        dracula, gruvbox-dark, kanagawa, nord, ayu-dark, tokyo-night
      '';
    };

    yazi_plugins = mkOption {
      type = types.listOf types.str;
      default = ["git" "starship"];
      description = "Yazi plugins to load (core plugins auto-layout and sidebar-status are always loaded)";
    };

    yazi_theme = mkOption {
      type = types.str;
      default = "default";
      description = "Yazi color theme";
    };

    yazi_sort_by = mkOption {
      type = types.enum ["alphabetical" "natural" "modified" "created" "size"];
      default = "alphabetical";
      description = "Default file sorting method";
    };

    debug_mode = mkOption {
      type = types.bool;
      default = false;
      description = "Enable verbose debug logging";
    };

    skip_welcome_screen = mkOption {
      type = types.bool;
      default = true;
      description = "Skip the welcome screen on startup";
    };

    ascii_art_mode = mkOption {
      type = types.enum ["static" "animated"];
      default = "static";
      description = "ASCII art display mode";
    };

    persistent_sessions = mkOption {
      type = types.bool;
      default = false;
      description = "Enable persistent Zellij sessions";
    };

    session_name = mkOption {
      type = types.str;
      default = "yazelix";
      description = "Session name for persistent sessions";
    };

    enable_atuin = mkOption {
      type = types.bool;
      default = false;
      description = "Enable Atuin shell history integration (disabled by default).";
    };

    user_packages = mkOption {
      type = types.listOf types.package;
      default = [];
      description = "Additional packages to install in Yazelix environment";
    };
  };

  config = mkIf cfg.enable (lib.mkMerge [
    {
      xdg.configFile."yazelix".source = configStore;
      home.packages = cfg.user_packages;
    }
    (lib.mkIf (lib.any (pkg: pkg == pkgs.glow) cfg.user_packages) {
      home.sessionVariables = {
        GLOW_BIN = "${pkgs.glow}/bin/glow";
      };
    })
    (lib.mkIf pkgs.stdenv.isLinux {
      # Desktop entry for application launcher (Linux only)
      xdg.desktopEntries.yazelix = {
        name = "Yazelix";
        comment = "Yazi + Zellij + Helix integrated terminal environment";
        exec = "${config.xdg.configHome}/yazelix/nushell/scripts/core/desktop_launcher.nu";
        icon = "yazelix";
        categories = ["Development"];
        type = "Application";
        startupWMClass = "com.yazelix.Yazelix";
      };
    })
  ]);
}
